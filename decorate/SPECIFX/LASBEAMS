///////////////////////////////////////
// LASER BEAMS for FORCEFIELDS ////////
// for REALM667 	       ////////
// by Tormentor667 	       ////////
// optimized by Blue Shadow    ////////
///////////////////////////////////////

// Horizontal laser beam ------------------------------------------------------

Actor BaseLaserHorizontal
{
  RenderStyle "Add"
  YScale 0.2
  XScale 0.6
  +FORCEXYBILLBOARD
  +ISMONSTER
  +NOINTERACTION
  States
  {
  Spawn:
    LBFX A -1 Bright
    Stop
  }
}

Actor BaseLaserHorizontalSpawner
{
  Radius 2
  Height 2
  +ISMONSTER
  +NOGRAVITY
  +THRUACTORS
  +SHOOTABLE
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1 A_SpawnItemEx("BaseLaserHorizontal", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION|SXF_SETMASTER|SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
    Goto Spawn+1
  StopSpawning:
    TNT1 A -1 A_ChangeFlag("NOINTERACTION", TRUE)
    Stop
  Death:
    TNT1 A 1 A_RemoveChildren(TRUE)
    Stop
  }
}

Actor LaserBeam_Green_Horizontal :  SwitchableDecoration 291
{
  +CLIENTSIDEONLY
  +ISMONSTER
  +NOINTERACTION
  +NOSECTOR
  States
  {
  Spawn:
  Active:
    TNT1 A 0
    TNT1 A 1 A_SpawnItemEx("BaseLaserHorizontalSpawner", 0, 0, 0, 4, 0, 0, 0, SXF_TRANSFERTRANSLATION|SXF_SETMASTER|SXF_CLIENTSIDE)
    TNT1 A -1
    Stop
  Inactive:
    TNT1 A -1 A_KillChildren
    Stop
  }
}

Actor LaserBeam_Blue_Horizontal : LaserBeam_Green_Horizontal 292
{
//$Category Laser beams
  Translation "112:127=196:207"
}

Actor LaserBeam_White_Horizontal : LaserBeam_Green_Horizontal 293
{
//$Category Laser beams
  Translation "112:127=80:111"
}

Actor LaserBeam_Red_Horizontal : LaserBeam_Green_Horizontal 294
{
//$Category Laser beams
  Translation "112:127=176:191"
}

Actor LaserBeam_Orange_Horizontal : LaserBeam_Green_Horizontal 295
{
//$Category Laser beams
  Translation "112:127=214:223"
}

// Vertical laser beam --------------------------------------------------------

Actor BaseLaserVertical : BaseLaserHorizontal
{
  YScale 0.6
  XScale 0.2
}

Actor BaseLaserVerticalSpawner : BaseLaserHorizontalSpawner
{
  States
  {
  Spawn:
    TNT1 A 0
    TNT1 A 1 A_SpawnItemEx("BaseLaserVertical", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION|SXF_SETMASTER|SXF_CLIENTSIDE|SXF_NOCHECKPOSITION)
    Goto Spawn+1
  }
}

Actor LaserBeam_Green_Vertical : LaserBeam_Green_Horizontal 281
{
//$Category Laser beams
  States
  {
  Spawn:
  Active:
    TNT1 A 0
    TNT1 A 1 A_SpawnItemEx("BaseLaserVerticalSpawner", 0, 0, 0, 0, 0, 4, 0, SXF_TRANSFERTRANSLATION|SXF_SETMASTER|SXF_CLIENTSIDE)
    TNT1 A -1
    Stop
  }
}

Actor LaserBeam_Blue_Vertical : LaserBeam_Green_Vertical 282
{
//$Category Laser beams
  Translation "112:127=196:207"
}

Actor LaserBeam_White_Vertical : LaserBeam_Green_Vertical 283
{
//$Category Laser beams
  Translation "112:127=80:111"
}

Actor LaserBeam_Red_Vertical : LaserBeam_Green_Vertical 284
{
//$Category Laser beams
  Translation "112:127=176:191"
}

Actor LaserBeam_Orange_Vertical : LaserBeam_Green_Vertical 285
{
//$Category Laser beams
  Translation "112:127=214:223"
}